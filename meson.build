project('wordofgod', 'c',
          version: '0.1.0',
    meson_version: '>= 0.62.0',
  default_options: [ 'warning_level=2', 'werror=false', 'c_std=gnu11', ],
)

i18n = import('i18n')
gnome = import('gnome')
cc = meson.get_compiler('c')

app_id = 'org.robertomorrison.wordofgod'
developers = 'robertomorrison'
developer_name = 'Roberto Morrison'


prefix = get_option('prefix')
datadir = prefix / get_option('datadir')
iconsdir = datadir / 'icons'


config_h = configuration_data()
config_h.set_quoted('PACKAGE_VERSION', meson.project_version())
config_h.set_quoted('GETTEXT_PACKAGE', 'wordofgod')
config_h.set_quoted('LOCALE_DIR', join_paths(get_option('prefix'), get_option('localedir')))
config_h.set_quoted('PACKAGE_ICON_NAME', app_id)
config_h.set_quoted('APP_ID', app_id)
config_h.set_quoted('DEVELOPERS', developers)
config_h.set_quoted('DEVELOPER_NAME', developer_name)
config_h.set_quoted('APP_NAME', 'Word of God')
configure_file(output: 'config.h', configuration: config_h)
add_project_arguments(['-I' + meson.project_build_root()], language: 'c')


subdir('data')
subdir('src')
subdir('po')

gnome.post_install(
     glib_compile_schemas: true,
    gtk_update_icon_cache: true,
  update_desktop_database: true,
)
